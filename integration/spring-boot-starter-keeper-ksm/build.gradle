plugins {
	id 'java-library'
	id 'maven-publish'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.keepersecurity'
version = '0.0.1-SNAPSHOT'

java {
        toolchain {
                languageVersion = JavaLanguageVersion.of(21)
        }
    withSourcesJar()
    withJavadocJar()
}

repositories {
	mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.4"
    }
}

dependencies {
    api 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'com.keepersecurity.secrets-manager:core:17.0.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'org.slf4j:slf4j-api'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    compileOnlyApi 'org.bouncycastle:bc-fips:2.1.0'
    compileOnlyApi 'org.bouncycastle:bcprov-jdk18on:1.78' 
    compileOnlyApi 'software.amazon.awssdk:kms:2.20.28'
    compileOnlyApi 'software.amazon.awssdk:auth:2.20.28'
    compileOnlyApi 'software.amazon.awssdk:secretsmanager:2.20.28'
    compileOnlyApi 'com.google.cloud:google-cloud-kms:2.62.0'
    compileOnlyApi 'com.google.cloud:google-cloud-secretmanager:2.29.0'
    compileOnlyApi 'com.google.auth:google-auth-library-oauth2-http:1.33.1'
    compileOnlyApi 'com.azure:azure-identity:1.15.0'
    compileOnlyApi 'com.azure:azure-security-keyvault-keys:4.9.2'
    compileOnlyApi 'com.azure:azure-security-keyvault-secrets:4.9.2'
    compileOnlyApi 'com.oracle.oci.sdk:oci-java-sdk-keymanagement:3.60.0'
    compileOnlyApi 'com.oracle.oci.sdk:oci-java-sdk-common-httpclient-jersey:3.60.0'
    compileOnlyApi 'com.oracle.oci.sdk:oci-java-sdk-common:3.60.0'

    // Only load Spring Cloud support if the application includes it
    compileOnlyApi 'org.springframework.cloud:spring-cloud-context:4.1.3'

    testImplementation 'org.springframework.cloud:spring-cloud-context:4.1.3'
    

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


tasks.named('test') {
	useJUnitPlatform()
}

// Ensure the jar is marked as a Spring Boot auto-configurable module (if needed, optional)
jar {
    archiveBaseName = 'spring-boot-starter-keeper-ksm'
}

publishing {
    repositories {
        maven {
            name = "keeper-central"
            credentials {
                username = System.getenv("GH_REPO_USER")
                password = System.getenv("GH_REPO_TOKEN")
            }
            url = uri("https://maven.pkg.github.com/Keeper-Security/keeper-central")
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'com.keepersecurity'
            artifactId = 'spring-boot-starter-keeper-ksm'
            version = project.version

            pom {
                name = 'Spring Boot Starter for Keeper Security'
                description = 'A Spring Boot starter that integrates with Keeper Secrets Manager using HSM-backed X25519 key decryption.'
                url = 'https://github.com/Keeper-Security/keeper-central'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'keeper'
                        name = 'Keeper Security'
                        email = 'support@keepersecurity.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/Keeper-Security/keeper-central.git'
                    developerConnection = 'scm:git:ssh://git@github.com/Keeper-Security/keeper-central.git'
                    url = 'https://github.com/Keeper-Security/keeper-central'
                }
            }
        }
    }
}

gradle.projectsEvaluated {
    configurations.compileOnly.incoming.beforeResolve {
        throw new GradleException("compileOnly is being resolved!")
    }
}